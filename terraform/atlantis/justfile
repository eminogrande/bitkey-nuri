account_id := "000000000000"

[private]
default:
  just --list

build:
  #!/usr/bin/env bash
  set -ex
  TERRAFORM_VERSION=$(hermit list --json | gojq -r 'map(select(.Reference.Name == "terraform")) | .[0].Reference.Version')
  TERRAGRUNT_VERSION=$(hermit list --json | gojq -r 'map(select(.Reference.Name == "terragrunt")) | .[0].Reference.Version')
  TERRAGRUNT_ATLANTIS_CONFIG_VERSION=$(hermit list --json | gojq -r 'map(select(.Reference.Name == "terragrunt-atlantis-config")) | .[0].Reference.Version')
  docker buildx build --load --platform=linux/amd64 --progress=plain -t atlantis:latest \
    --build-arg=TERRAFORM_VERSION=${TERRAFORM_VERSION} \
    --build-arg=TERRAGRUNT_VERSION=${TERRAGRUNT_VERSION} \
    --build-arg=ATLANTIS_VERSION=${ATLANTIS_VERSION} \
    --build-arg=TERRAGRUNT_ATLANTIS_CONFIG_VERSION=${TERRAGRUNT_ATLANTIS_CONFIG_VERSION} \
    .

push tag="":
  #!/usr/bin/env bash
  set -ex
  version="{{tag}}"
  if [ -z "${version}" ]; then
    version="$(git rev-parse HEAD)"
  fi
  docker_tag={{account_id}}.dkr.ecr.us-west-2.amazonaws.com/atlantis:$version
  docker tag atlantis:latest "${docker_tag}"
  aws ecr get-login-password --region us-west-2  | docker login --username AWS --password-stdin {{account_id}}.dkr.ecr.us-west-2.amazonaws.com
  docker push "${docker_tag}"

update-tag tag="":
  #!/usr/bin/env bash
  set -ex
  version="{{tag}}"
  if [ -z "${version}" ]; then
    version="$(git rev-parse HEAD)"
  fi
  cd ..
  sed -i.bak -E "s/atlantis_tag[[:space:]]+=.*/atlantis_tag = \"${version}\"/" aws/bitkey/prod/shared/atlantis/service/terragrunt.hcl
  terragrunt hclfmt
  rm aws/bitkey/prod/shared/atlantis/service/terragrunt.hcl.bak

logs:
  aws logs tail --follow atlantis | grep -v -E 'scheduled|healthz' | cut -d ' ' -f 3-
